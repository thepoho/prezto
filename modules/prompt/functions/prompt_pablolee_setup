function prompt_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_precmd

  # Use extended color pallete if available.
  _prompt_colors=(
    "%F{37}" # branch
    "%F{124}" # deleted
    "%F{136}" # untracked
    "%F{245}" # modified
    "%F{64}" # added
    "%F{245}" prompt
  )

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${_prompt_colors[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${_prompt_colors[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${_prompt_colors[2]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${_prompt_colors[4]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${_prompt_colors[3]}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  # Define prompts.
  PROMPT="${_prompt_colors[6]}%~%f "'${git_info:+${(e)git_info[prompt]}}'"$ "
  RPROMPT=''
}

prompt_setup "$@"
